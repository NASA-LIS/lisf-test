C **  SUBROUTINE CALCULATES SNOW DEPTH

      SUBROUTINE SNDEPTH(WE,SLIQ,SFALL,SMELT,SGMLOS,SRFRZ,
     +                        TA,DTA,DT,SNDPT,SNDEN,SNTMP)
C
C....................................................................
C
C  SUBROUTINE CALCULATES SNOW DEPTH AND SNOW TEMPERATURE FOR THE 
C    COMPUTATIONAL TIME PERIOD
C
C     INITIALLY WRITTEN BY VICTOR KOREN IN APRIL 2000.
C
C     MODIFIED 11/05 BY E. ANDERSON
C       - VARIABLE NAMES CHANGED TO BE CONSISTENT FROM ONE ROUTINE TO
C         THE NEXT.
C       - SIGNIFICANT CHANGES TO COMPUTATIONAL SEQUENCE
C
C....................................................................
C     INPUT VARIABLES
C **  WE     - WATER EQUIVALENT, MM
C **  SLIQ   - LIQUID WATER CONTENT, MM
C **  SFALL  - NEW SNOWFALL, MM
C **  SMELT  - SURFACE MELT, MM
C **  SGMLOS - GROUND MELT, MM
C **  SRFRZ  - REFROZEN MELT/RAIN WATER, MM
C **  TA     - AIR TEMPERATURE, C
C **  DTA    - AIR TEMPERATURE CHANGE FROM THE PREVIOUS TIME PERIOD  
C **  DT     - TIME STEP OF THE COMPUTATIONAL PERIOD, HR
C **  SNDPT  - SNOW DEPTH, CM
C **  SNDEN  - SNOW DENSITY, G/CM3 (ICE PORTION OF SNOW COVER)
C **  SNTMP  - AVERAGE SNOW TEMPERATURE, C
C **  IBUG   - DEBUG FLAG
C
C     OTHER VARIABLES
C **  TSNEW  - TEMP OF NEW SNOW, C
C **  DPTNEW - DEPTH OF NEW SNOW, CM
C **  DENNEW - DENSITY OF NEW SNOW, G/CM3
C **  DCM    - DENSITY INCREASE DUE TO MELT METAPHORISM
C                (REFREEZING OF LIQUID WATER), MULTIPLYING FACTOR
C **  WE1    - VALUE OF WE AT THE START OF THE PERIOD, MM
C **  WEX    - WATER EQUIVALENT OF EXISTING SNOW AT END OF PERIOD, MM
C **  DPTOLD - DEPTH OF OLD SNOW AFTER ANY MELT, CM
C **  TSNDPT - TOTAL DEPTH (OLD AND NEW SNOW), CM
C **  TSNDEN - TOTAL DENSITY (OLD AND NEW SNOW), G/CM3
C **  SFALLX - SNOWFALL MINUS MELT, MM
C **  PLOSS  - TOTAL MELT DURING THE PERIOD - SURFACE MELT PLUS
C                GROUNDMELT, MM
C
C  VALUES INPUT TO THE SUBROUTINE
C    WE, SLIQ - VALUE AT THE END OF THE CURRENT COMPUTATIONAL 
C       TIME PERIOD
C    SFALL, SMELT, SGMLOS, SRFRZ - AMOUNTS THAT OCCUR DURING THE PERIOD
C    TA - PERIOD AVERAGE VALUE
C    SNDPT, SNDEN, SNTMP - VALUE AT THE BEGINNING OF THE PERIOD
C  VALUES OUTPUT
C    SNDPT, SNDEN, SNTMP - VALUE AT THE END OF THE PERIOD
C
C    ASSUME TEMPERATURE OF ANY NEW SNOW = AVG. AIR TEMPERATURE
C      AS FAR AS COMPUTING THE DENSITY OF NEW SNOW - ACTUAL
C      TEMPERATURE OF NEW SNOW CANNOT EXCEED 0.0 DEGC.
      
      DPTNEW=0.0
      DENNEW=0.0
      TSNEW=TA
      IF (SNDPT.GT.0.0) GO TO 100
C********************************************************************
C********************************************************************
C    NEW SNOW COVER - NONE EXISTED AT START OF PERIOD
C      KNOWN - WE.GT.0.0 (IF NOT, ROUTINE WOULDN'T BE CALLED)
C            - SFALL.GT.0 (SINCE SNOW EXISTS AND THERE WAS NONE
C                AT THE START OF THE PERIOD)
C     COMPUTE NET SNOWFALL DURING THE PERIOD
C        - SNOWFALL MINUS ANY SURFACE AND GROUND MELT.
C        - NET SNOWFALL POSITIVE OR NO SNOW WOULD EXIST
      SFALLX=SFALL-SMELT-SGMLOS
C     COMPUTE DEPTH AND DENSITY OF NET SNOWFALL
      CALL SNEW(TSNEW,SFALLX,DPTNEW,DENNEW)
C     ACCOUNT FOR ANY REFREEZE - INCREASES DENSITY - RECOMPUTE DEPTH
      IF (SRFRZ.GT.0.0) THEN
        DCM=(SFALLX+SRFRZ)/SFALLX
        DENNEW=DCM*DENNEW
        DPTNEW=(0.1*WE)/DENNEW
      ENDIF
C     SET TOTAL SNOW COVER VALUES AT END OF PERIOD.
      SNDPT=DPTNEW
      SNDEN=DENNEW
      SNTMP=TSNEW
      IF (SNTMP.GT.0.0) SNTMP=0.0
      RETURN
C********************************************************************
C********************************************************************
C    SNOW COVER EXISTED AT START OF PERIOD
C     COMPUTE CHANGE IN DENSITY AND DEPTH OF EXISTING COVER
C     INCORPORATE EFFECT OF ANY NEW SNOW DURING THE PERIOD
C
C     COMPUTE NET MELT DURING PERIOD
  100 PLOSS=SMELT+SGMLOS
C     COMPUTE WE VALUE AT THE START OF THE PERIOD - EXISTING SNOW
      WE1=WE-SFALL+PLOSS-SRFRZ
C     CHECK FOR VARIOUS CASES
      IF (PLOSS.LT.WE1) THEN
C...............................................................
C     AMOUNT OF MELT IS LESS THAN EXISTING SNOW AT START OF PERIOD
C       ASSUME ALL MELT CAME FROM EXISTING SNOW - TYPICAL CASE
C     CHECK FOR ANY NEW SNOWFALL DURING PERIOD
C       IF ANY, GET DEPTH AND DENSITY
        IF (SFALL.GT.0.0) CALL SNEW(TSNEW,SFALL,DPTNEW,DENNEW)
C     COMPUTE WATER EQUIVALENT OF EXISTING SNOW AT END OF PERIOD
C       - PRIOR TO ANY REFREEZE
        WEX=WE1-PLOSS
C     COMPUTE DEPTH OF REMAINING OLD SNOW - ASSUME DENSITY OF
C       MELTED SNOW WAS THE SAME AS THE AVERAGE DENSITY OF THE
C       TOTAL OLD SNOW COVER
        DPTOLD=(0.1*WEX)/SNDEN
C     USE DENSITY OF COMBINED OLD AND NEW SNOW TO COMPUTE THE
C       THERMAL CONDUCTIVITY TO USE FOR CALCULATING THE CHANGE
C       IN THE TEMPERATURE OF THE SNOW COVER.
        TSNDPT=DPTOLD+DPTNEW
        TSNDEN=(DPTOLD*SNDEN+DPTNEW*DENNEW)/TSNDPT
C     COMPUTE NEW TEMPERATURE OF THE OLD SNOW
        CALL SNOWT(TSNDPT,TSNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,DT)
C     COMPUTE NEW DEPTH AND DENSITY OF OLD SNOW AFTER EFFECT OF
C       COMPACTION AND DESTRUCTIVE METAMORPHISM ARE TAKEN INTO
C       ACCOUNT.
        CALL SNOWPACK(WEX,DT,SNDPT,SNDEN,SLIQ,SNTMP)
      ELSE
        SFALLX=SFALL-PLOSS
        IF (SFALLX.GT.0.0) THEN
C...............................................................
C       MELT DURING THE PERIOD WAS GREATER THAN THE AMOUNT OF SNOW
C         AT THE START OF THE PERIOD AND NEW SNOWFALL EXCEEDS THE
C         AMOUNT OF MELT DURING THE PERIOD - ASSUME MELT COMES FROM
C         NEW SNOWFALL
C         GET DEPTH AND DENSITY OF NEW SNOW
          CALL SNEW(TSNEW,SFALLX,DPTNEW,DENNEW)
C         USE DENSITY OF COMBINED OLD AND NEW SNOW TO COMPUTE THE
C           THERMAL CONDUCTIVITY TO USE FOR CALCULATING THE CHANGE
C           IN THE TEMPERATURE OF THE SNOW COVER.
          TSNDPT=SNDPT+DPTNEW
          TSNDEN=(SNDPT*SNDEN+DPTNEW*DENNEW)/TSNDPT
C         COMPUTE NEW TEMPERATURE OF THE OLD SNOW
          CALL SNOWT(TSNDPT,TSNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,DT)
C         COMPUTE NEW DEPTH AND DENSITY OF OLD SNOW AFTER EFFECT OF
C           COMPACTION AND DESTRUCTIVE METAMORPHISM ARE TAKEN INTO
C           ACCOUNT.
          CALL SNOWPACK(WE1,DT,SNDPT,SNDEN,SLIQ,SNTMP)
        ELSE
C...............................................................
C       SFALLX IS .LE. 0.0, THUS MELT EXCEEDS BOTH EXISTING SNOW
C         AT THE START OF THE PERIOD AND THE NEW SNOWFALL, BUT NOT
C         THE TOTAL OF THE TWO QUANTITIES.  ASSUME ALL NEW SNOW
C         MELTS AND ONLY PART OF THE EXISTING PACK REMAINS.
          WEX=WE-SRFRZ
C         DEPTH OF EXISTING SNOW THAT REMAINS
          TSNDPT=(0.1*WEX)/SNDEN
C         COMPUTE NEW TEMPERATURE OF THE REMAINING OLD SNOW
          CALL SNOWT(TSNDPT,SNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,DT)
C         COMPUTE NEW DEPTH AND DENSITY OF REMAINING OLD SNOW AFTER
C           EFFECT OF COMPACTION AND DESTRUCTIVE METAMORPHISM ARE
C           TAKEN INTO ACCOUNT.
          CALL SNOWPACK(WEX,DT,SNDPT,SNDEN,SLIQ,SNTMP)
        ENDIF
      ENDIF
C...............................................................
C     IN ALL CASES WHEN SNOW EXISTED AT THE START OF THE PERIOD
C       COMPUTE NEW TOTAL DEPTH AND TEMPERATURE BY WEIGHING BOTH
C       THE OLD AND NEW SNOW.
      IF (TSNEW.GT.0.0) TSNEW=0.0
      SNTMP=(SNTMP*SNDPT+TSNEW*DPTNEW)/(SNDPT+DPTNEW)
      SNDPT=SNDPT+DPTNEW
      SNDEN=(0.1*(WE-SRFRZ))/SNDPT
C     COMPUTE EFFECT OF MELT METAMORPHISM - REFREEZING OF LIQUID
C       WATER WITHIN THE SNOW COVER.
      IF (SRFRZ.EQ.0.0) GO TO 110
C     COMPUTE INCREASE IN DENSITY DUE TO REFREEZING
      DCM=WE/(WE-SRFRZ)
      SNDEN=SNDEN*DCM
C     CHECK THAT DENSITY IS WITHIN THE RANGE OF 0.05 TO 0.60
  110 IF (SNDEN.GT.0.6) SNDEN=0.6
      IF (SNDEN.LT.0.05) SNDEN=0.05
C     COMPUTE SNOW DEPTH AT THE END OF THE PERIOD
      SNDPT=(0.1*WE)/SNDEN
C      
      RETURN
      END

C  SUBROUTINE CALCULATES AVERAGE SNOW TEMPERATURE
C
      SUBROUTINE SNOWT(SH,DS,WE,SLIQ,TA,DTA,TSNOW,DHC,DT)

C  SPECIFIC HEAT CAPACITIES OF ICE, WATER, AND AIR
      PARAMETER (CICE=2.1E06,CH2O=4.2E06,CAIR=1E03)
C  HEAT WAVE LENGTH, IN SECONDS
cvk 06/04/07      PARAMETER (WAVEL=43200)

       WAVEL = 2.0*3600.*DT  
       SHX=0.01*SH
       DHCX=0.01*DHC
       STOT=WE+SLIQ
       DST=0.1*STOT/SH 
C      Added by Shugong to deal with abnormal density due to small number 
C      of SH. Set the upper limit of DST = 0.5, adjust WE and SLIQ according to SH  
       if (DST>0.5) then
        DST = 0.5
        STOT_shugong =SH * DST * 10
        WE = WE * (STOT_shugong/STOT)
        SLIQ = SLIQ * (STOT_shugong/STOT)
       endif
       SL=0.0442*EXP(5.181*DST)
       FL=SLIQ/STOT
       SC=CICE*DS+CAIR*(1.-DS-FL)+CH2O*FL
       ALP=SQRT(3.14*SC/(WAVEL*SL))
       IF(DHC .GT. 0.) THEN

cvk 6/2010 introduced checking on difference between DHCX and SHX
cvk        to prevent division 0.0/0.0 
cvk        TSNOW=TSNOW+DTA*((EXP(-ALP*DHCX)-EXP(-ALP*SHX))/
cvk     +        (ALP*(SHX-DHCX)))
        sdif=shx-dhcx
        if(abs(sdif) .le. 1E-05) then
ccc vk 2012 chnges         texp=1.0
         shmax=shx
         if(dhcx .gt. shx) shmax=dhcx
         TSNOW=TSNOW+DTA*EXP(-ALP*shmax)
        else
         TSNOW=TSNOW+DTA*((EXP(-ALP*DHCX)-EXP(-ALP*SHX))/
     +                                  (ALP*(SHX-DHCX))) 
ccc vk 2012 chnges          texp=exp(alp*sdif-log(alp*sdif))-1.0/(alp*sdif)
ccc vk 2012 chnges         else
ccc vk 2012 chnges          texp=(exp(alp*sdif)-1.0)/(alp*sdif)
ccc vk 2012 chnges         endif
        endif
ccc vk 2012 chnges        tsnow=tsnow+dta*exp(-alp*shx)*texp
       ELSE
        TSNOW=TSNOW+DTA*((1.-EXP(-ALP*SHX))/(ALP*SHX))
       ENDIF 
      IF(TSNOW .GT. 0.) TSNOW=0.
      RETURN
      END

      SUBROUTINE SNEW ( T,P,HCN,DHN )
     
C  CALCULATING SNOW DEPTH AND DENSITITY OF THE NEW SNOWFALL
C      T   - AIR TEMPERATURE, C
C      P   - NEW SNOWFALL, MM 
C      HCN - SNOW DEPTH, CM
C      DHN - SNOW DENSITY
C   NEW VALUES OF SNOW DEPTH & DENSITY WILL BE RETURNED   

C ----------------------------------------------------------------------
C ***  CALCULATING NEW SNOWFALL DENSITY DEPENDING ON TEMPERATURE      **
C ***  EQUATION FROM ANDERSON - NOAA TECH REPORT NWS 19
C ***  BASED ON DATA FROM ALTA, UTAH
C-----------------------------------------------------------------------

C  CONVERT IN CALCULATION UNITS
      PX=0.1*P
      
      IF(T.LE.-15.) THEN
        DHN=0.05 
      ELSE                                                      
        DHN=0.05+0.0017*(T+15.)**1.5
      ENDIF

C **   CALCULATE CHANGE OF SNOW DEPTH       

      HCN=PX/DHN
      
      RETURN
      END      
      
      SUBROUTINE SNOWPACK (WE,DT,SNDPT,SNDEN,SLIQ,SNTMP)
C
C ****************************************************************
C **  SUBROUTINE CALCULATES SNOW COMPACTION AND DESTRUCTIVE    ***
C **  METAMORPHISM - ALSO COMPONENT OF MELT METAMORPHISM DUE   ***
C **  TO THE PRESENCE OF LIQUID WATER IS ACCOUNTED FOR.        ***
C **  EQUATIONS FOR INCREASING SNOW DENSITY WERE OBTAINED AS   ***
C **  APPROXIMATE SOLUTIONS OF E. ANDERSON DIFFERENTIAL        ***
C **  EQUATIONS (3.29) AND (3.30), (3.31), NOAA TECHNICAL      *** 
C **  REPORT NWS 19, by   VICTOR KOREN   03/25/95              ***
C **                                                           ***
C **  MODIFIED 11/05 BY E. ANDERSON -                          ***
C **    SOME VARIABLE NAMES CHANGED TO PROVIDE CONSISTENCY     ***
C **    EFFECT OF SNOWFALL, MELT, AND REFREEZE ON DEPTH NOW    ***
C **    MOVED UP TO CALLING ROUTINE - SNDEPTH19                ***
C ****************************************************************
C
C ***************************************************************
C  WE     - WATER EQUIVALENT OF THE ICE PORTION OF THE        ***
C            REMAINING PART OF THE SNOW COVER THAT EXISTED    ***
C            AT THE START OF THE COMPUTATIONAL PERIOD, MM     ***
C  DT     - LENGTH OF COMPUTATIONAL TIME PERIOD, HRS          ***
C  SNDPT  - SNOW DEPTH, CM                                    ***
C  SNDEN  - SNOW DENSITY, G/CM3                               ***
C  SLIQ   - LIQUID WATER CONTENTS, MM (END OF PERIOD)         ***
C  SNTMP  - AVERAGE SNOW TEMPERATURE OF REMAINING EXISTING    ***
C             SNOW COVER, C (END OF PERIOD)                   ***
C                                                             ***
C   INPUT -SNDPT AND SNDEN AT BEGINNING OF PERIOD             ***
C   OUTPUT - SNDPT AND SNDEN - VALUES FOR REMAINING EXISTING  ***
C     SNOW AFTER COMPACTION AND DESTRUCTIVE METAMORPHISM      ***
C ***************************************************************
C
      REAL*8 B
C
C  CONSTANTS OF SNOW COMPACTION AND DESTRUCTIVE METAMORPHISM
C    MODELS FROM ANDERSON - TECHNICAL REPORT 19:
C    C1 IN 1/(CM HR), C2 IN CM3/G, C3 IN 1/HR, C4 IN 1/DEGREE C,
C    C5 DIMENSIONLESS, THRESD IN G/CM3, CX IN CM3/G
C    ONLY PARAMETER C5 WAS CHANGED FROM 0.15 TO 0.20 BASED ON
C    RESULTS OF SIMULATIONS USING NOAA-ARS COOPERATIVE SNOW 
C    RESEARCH PROJECT DATA 
CEA  BASED ON COMPARISON AT ALL SNOWMIP LOCATIONS AND SEVERAL
CEA    OTHER SITES THE SNOW METAMORPHISM PARAMETERS WERE FURTHER
CEA    MODIFIED TO PRODUCE IMPROVED RESULTS OVER A WIDER RANGE OF
CEA    CLIMATOLOGICAL CONDITIONS.
      PARAMETER (C1=0.026, C2=21.0)
      PARAMETER (C3=0.005, C4=0.10, THRESD=0.15, C5=2.,CX=23.)
C
C **  CONVERT WATER EQUIVALENT TO CENTIMETERS
      SNDPT1=SNDPT
      SNDEN1=SNDEN
      WECM=WE*0.1
C
C **  CALCULATE CHANGE IN DENSITY AS A RESULT OF COMPACTION
C **  C1 IS THE FRACTIONAL INCREASE IN DENSITY (1/(CM*HR)) 
C **  C2 IS A CONSTANT (CM3/G) Kojima estimated as 21 cms/g
C   
      DCC=1.
      IF(WECM.GT.1E-2) THEN
        B=DT*C1*EXP(0.08*SNTMP-C2*SNDEN)
        DCC=(DEXP(B*WECM)-1.)/(B*WECM)
      ENDIF
C
C **  CALCULATE THE DENSITY CHANGE AS A RESULT OF DESTRUCTIVE
C       METAMORPHISM AND COMPONENT OF MELT METAPHORISM DUE TO
C       THE PRESENCE OF LIQUID WATER
C     C3 IS THE FRACTIONAL SETTLING RATE AT 0 DEGREE FOR DENSITIES 
C        LESS THAN THRESHOLD DENSITY, THRESD
C     C4 IS A CONSTANT 
C     C5 IS AN EMPIRICAL ADJUSTMENT FACTOR THAT ACCOUNTS FOR
C        THE PRESENCE OF LIQUID WATER
C 
      A1=C3
      IF(SLIQ .GT. 0.) A1=A1*C5
      A2=C4*SNTMP
      IF(SNDEN.GT.THRESD) A2=A2-CX*(SNDEN-THRESD)
      A=A1*DT*EXP(A2)
      DCD=EXP(A)

C **  NEW SNOW DENSITY OF EXISTING SNOW AS A RESULT OF COMPACTION
C **   AND DESTRUCTIVE METAMORPHISM 
      SNDEN=SNDEN*DCC*DCD
C **  NEW DEPTH OF EXISTING SNOW AFTER DENSITY CHANGE
      SNDPT=WECM/SNDEN
      RETURN
      END     
